plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "me.yanglw.android.tv"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            if (project.hasProperty("SIGNING_STORE_FILE")) {
                storeFile file(SIGNING_STORE_FILE)
                storePassword SIGNING_STORE_PASSWORD
                keyAlias SIGNING_KEY_ALIAS
                keyPassword SIGNING_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "target"
    productFlavors {
        mars {
            dimension "target"
            def target_package = "com.xiaojie.tv"
            applicationIdSuffix ".$target_package"
            manifestPlaceholders = [TARGET_PACKAGE_NAME: target_package]
            buildConfigField "String", "TARGET_PACKAGE_NAME", "\"$target_package\""
        }
        tx {
            dimension "target"
            def target_package = "com.ktcp.video"
            applicationIdSuffix ".$target_package"
            manifestPlaceholders = [TARGET_PACKAGE_NAME: target_package]
            buildConfigField "String", "TARGET_PACKAGE_NAME", "\"$target_package\""
        }
        iqiyi {
            dimension "target"
            def target_package = "com.gitvdemo.video"
            applicationIdSuffix ".$target_package"
            manifestPlaceholders = [TARGET_PACKAGE_NAME: target_package]
            buildConfigField "String", "TARGET_PACKAGE_NAME", "\"$target_package\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        outputFileName = "${variant.applicationId}.apk"
    }
}
